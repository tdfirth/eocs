// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 *
 * if      (reset[t] == 1) out[t+1] then sel = 11
 * else if (load[t] == 1)  out[t+1] then sel = 10
 * else if (inc[t] == 1)   out[t+1] then sel = 01
 * else                    out[t+1] then sel = 00
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    //Handle inc
    Mux16(a=registerOut, b=addOut, sel=inc, out=incOut);

    //Handle load
    Mux16(a=incOut, b=in, sel=load, out=loadOut);

    //Handle reset
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);

    //Handle register
    Register(in=resetOut, load=true, out=registerOut, out=out);

    //Handle addition and pass back into next iteration
    Add16(a=registerOut, b[0]=true, out=addOut);
}
